apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

def getProperty(String filename, String propName) {
    def propsFile = rootProject.file(filename)
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props[propName] != null) {
            return props[propName]
        } else {
            print("No such property " + propName + " in file " + filename);
        }
    } else {
        print(filename + " does not exist!")
    }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")

def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.ratik.uttam"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 17
        versionName "4.0"

        buildConfigField "String", "CLIENT_ID", "\"${getProperty("local.properties", "client_id")}\""
    }

    buildTypes {
        release {
            8
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "default"

    productFlavors {
        dev {
            applicationId "com.ratik.uttam.dev"
        }
        prod {
            applicationId "com.ratik.uttam.prod"
        }
    }
}

dependencies {
    // AppCompat
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:design:27.1.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.8'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // RxBindings
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'

    // RxPermissions
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'

    // Dagger 2
    implementation 'com.google.dagger:dagger:2.12'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.12'

    // App Tour
    implementation 'com.vlonjatg.android:app-tour:1.0'

    // Butter Knife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Billing
    prodImplementation 'org.solovyev.android:checkout:1.2.1'

    // Ads
    prodImplementation 'com.google.firebase:firebase-ads:11.4.2'

    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.10.0'
}
